# Generic colour asb-bar prompt theme from bashprompt
# Created by Jim Foltz <aa204@acorn.net>
# Changed by Spidey 08/06
# Converted to zsh prompt theme by <adam@spiers.net>

# Messed with by ASB @ Wed Jul  8 18:04:27 IST 2015

prompt_asb_help () {
  cat <<EOH
This prompt is color-scheme-able.  You can invoke it thus:

  prompt asb [<asb-bar-and-cwd> [<userhost> [<date>]]] 

where the parameters are the colors for the asb-bar and current
working directory, user@host text, and date text respectively.  The
default colors are green, white, and white.  This theme works best
with a dark background.

Recommended fonts for this theme: either UTF-8, or nexus or vga or similar.
If you don't have any of these, the 8-bit characters will probably look
stupid.
EOH
}

# emacs shell-script mode gets confused with ' in heredoc above

prompt_asb_setup () {
  local asbbar_cwd=${1:-'green'}
  local userhost=${2:-'white'}
  local date=${3:-'white'}

  local -A schars
  autoload -Uz prompt_special_chars
  prompt_special_chars

  PS1="%F{$asbbar_cwd}%B%K{$asbbar_cwd}$schars[333]$schars[262]$schars[261]$schars[260]%F{$userhost}%K{$asbbar_cwd}%B%n@%m%b%F{$asbbar_cwd}%K{black}$schars[333]$schars[262]$schars[261]$schars[260]%F{$date}%K{black}%B %D{%a %b %d} %D{%I:%M:%S%P} %F{$asbbar_cwd}%K{black}%B%~ >>> %b%k%f"
  PS2="%F{$asbbar_cwd}%K{black}$schars[333]$schars[262]$schars[261]$schars[260]%B +++ %b%k%f"

  prompt_opts=(cr subst percent)
}

prompt_asb_preview () {
  local color colors
  colors=(red yellow green blue magenta)

  if (( ! $#* )); then
    for (( i = 1; i <= $#colors; i++ )); do
      color=$colors[$i]
      prompt_preview_theme asb $color
      print
    done
    prompt_preview_theme asb white grey blue
  else
    prompt_preview_theme asb "$@"
  fi
}

prompt_asb_setup "$@"
